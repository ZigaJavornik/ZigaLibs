<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.14">
  <POU Name="FB_AlarmManager" Id="{66ff5c63-0690-045e-3836-b8992628f846}" SpecialFunc="None">
    <Declaration><![CDATA[// Alarm manager function block. 
// Make sure to call the body of this block before doing any method calls so that the array to be managed is assigned via the var_in_out.
// If you, for example, register an alarm before assigning the array, the alarm will not be registered due to invalid reference of the array.
FUNCTION_BLOCK FB_AlarmManager IMPLEMENTS I_AlarmManager

VAR
	_enable						: BOOL;
	_alarmArrayProperties		: ST_ArrayProperties;
	_stringBuilder				: FB_StringBuilder;
	_dateTime					: FB_LocalSystemTimeProvider;
	_initialized				: BOOL := FALSE;
	
	_activeAlarmCount			: UINT;
	_unacknowledgedAlarmCount	: UINT;
	_logger						: REFERENCE TO I_LoggerEx;	// If provided, logging will be used.
	_emptyAlarm					: ST_AlarmInfo;
	_registeredAlarmsCount		: UDINT;
	_alarmActionBuilder			: FB_AlarmActionBuilder(THIS^);
END_VAR

VAR_IN_OUT
	// Array of alarms which the alarm manager will manage.
	refAlarms : ARRAY[*] OF ST_AlarmInfo;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[_dateTime();
_alarmArrayProperties.StartIndex := LOWER_BOUND(refAlarms, 1);
_alarmArrayProperties.EndIndex := UPPER_BOUND(refAlarms, 1);
_alarmArrayProperties.Count := (_alarmArrayProperties.EndIndex - _alarmArrayProperties.StartIndex) + 1;]]></ST>
    </Implementation>
    <Folder Name="Constructor" Id="{540d7602-4dd8-0316-1d8f-b030614fbb56}" />
    <Folder Name="Methods - Private" Id="{2081bf70-075e-0c16-2099-9284fdb2b80a}" />
    <Folder Name="Methods - public" Id="{0151f663-d300-0140-0e71-d12dddb36b94}">
      <Folder Name="Actions - ById" Id="{24586308-ecc5-0e50-2fa8-9135d7fff715}" />
      <Folder Name="Actions - ByName" Id="{e2bcd09c-87db-03cf-333a-5fca6bd7ba00}" />
    </Folder>
    <Folder Name="Properties - public" Id="{0e5f18d4-d4f9-0c87-2317-22c815bdc375}" />
    <Method Name="AcknowledgeAlarm" Id="{49bf6711-a2a3-0bae-1c07-9de351f9ccfe}" FolderPath="Methods - Private\">
      <Declaration><![CDATA[METHOD INTERNAL AcknowledgeAlarm
VAR_INPUT
	alarm : REFERENCE TO ST_AlarmInfo;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF alarm.IsAcknowledged THEN
	RETURN;
END_IF

_unacknowledgedAlarmCount := _unacknowledgedAlarmCount - 1;
alarm.IsAcknowledged := TRUE;
alarm.TimeInfos.Acknowledged := _dateTime.CurrentTime.Value;

IF __ISVALIDREF(_logger) THEN 
	_logger.LogInfo(_stringBuilder.Reset()
		.AddString('Alarm ')
		.AddWithDblQuotationMarks(str := alarm.Name)
		.AddString(' with Id ')
		.AddWithDblQuotationMarks(str := TO_STRING(alarm.Id))
		.AddString(' acknowledged.')
		.Create());
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="AcknowledgeAlarmById" Id="{734f7742-a0fc-0dc5-34e4-5ca1d673303d}" FolderPath="Methods - public\Actions - ById\">
      <Declaration><![CDATA[(* Acknowledges the alarm with the provided id*)
METHOD AcknowledgeAlarmById
VAR_INPUT
	nId	: T_ArrayIndexer;
END_VAR

VAR
	foundAlarm	: ST_AlarmInfo;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT __ISVALIDREF(GetAlarmById(nID := nId)) THEN
	RETURN;
END_IF

foundAlarm := GetAlarmById(nID := nId);
IF foundAlarm.Id = nId THEN
	AcknowledgeAlarm(alarm := GetAlarmById(nID := nId));
	RETURN;
END_IF


]]></ST>
      </Implementation>
    </Method>
    <Method Name="AcknowledgeAlarmByName" Id="{65c0a185-eb5e-0a64-3ad5-34d1d325dc4a}" FolderPath="Methods - public\Actions - ByName\">
      <Declaration><![CDATA[(* Acknowledges the alarm with the provided name.*)
METHOD AcknowledgeAlarmByName
VAR_INPUT
	sName	: Tc2_System.T_MaxString;
END_VAR

VAR
	foundAlarm	: ST_AlarmInfo;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT __ISVALIDREF(GetAlarmByName(sName := sName)) THEN
	RETURN;
END_IF

foundAlarm := GetAlarmByName(sName := sName);
IF foundAlarm.Name = sName THEN
	AcknowledgeAlarm(alarm := GetAlarmByName(sName := sName));
	RETURN;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="AcknowledgeAllAlarms" Id="{5b392a2e-2d60-0a96-2f73-81bedda5c678}" FolderPath="Methods - public\">
      <Declaration><![CDATA[(* Acknowledges all uacknowledged alarms.*)
METHOD AcknowledgeAllAlarms
VAR
	alarmIndex	: T_ArrayIndexer;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT THIS^.ValidateArrayRef() THEN
	RETURN;
END_IF

FOR alarmIndex := _alarmArrayProperties.StartIndex TO GetMaxAlarmArrayIndex() BY 1 DO
	AcknowledgeAlarm(alarm := refAlarms[alarmIndex]);
END_FOR]]></ST>
      </Implementation>
    </Method>
    <Method Name="Actions" Id="{380b72fb-4b0d-03f2-00dd-30b54fd3fcc1}">
      <Declaration><![CDATA[// Provides access to action builder with fluent approach.
METHOD PUBLIC Actions : I_AlarmActions
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[Actions := _alarmActionBuilder;]]></ST>
      </Implementation>
    </Method>
    <Property Name="ActiveAlarmsCount" Id="{be420905-14e7-0fad-06c4-6349c17a27aa}" FolderPath="Properties - public\">
      <Declaration><![CDATA[{attribute 'monitoring':='call'}
(* Gets the total count of currently active alarms*)
PROPERTY ActiveAlarmsCount : UINT
]]></Declaration>
      <Get Name="Get" Id="{788d388c-36b3-0e58-1083-b8d4f609d13c}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[ActiveAlarmsCount := _activeAlarmCount;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="DeactivateAlarm" Id="{51bb9f4e-cec0-0ccf-1670-a8d995654248}" FolderPath="Methods - Private\">
      <Declaration><![CDATA[METHOD INTERNAL DeactivateAlarm
VAR_INPUT
	alarm : REFERENCE TO ST_AlarmInfo;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT alarm.IsActive THEN
	RETURN;
END_IF

alarm.IsActive := FALSE;
alarm.TimeInfos.Deactivated := _dateTime.CurrentTime.Value;
_activeAlarmCount := _activeAlarmCount - 1;

IF __ISVALIDREF(_logger) THEN
	_logger.LogInfo(_stringBuilder
		.Reset()
		.AddString('Alarm ')
		.AddWithDblQuotationMarks(str := alarm.Name)
		.AddString(' with Id ')
		.AddWithDblQuotationMarks(str := TO_STRING(alarm.Id))
		.AddString(' deactivated.')
		.Create());
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="DeactivateAlarmById" Id="{95d37b42-a850-0394-240a-fb99f44b0a7d}" FolderPath="Methods - public\Actions - ById\">
      <Declaration><![CDATA[(* Deactivates the alarm with the provided Id (if it exists).*)
METHOD DeactivateAlarmById
VAR_INPUT
	(* Alarm id to be deactivated.*)
	nId	: T_ArrayIndexer;
END_VAR

VAR
	foundAlarm	 : ST_AlarmInfo;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT __ISVALIDREF(GetAlarmById(nID := nId)) THEN
	RETURN;
END_IF

foundAlarm := GetAlarmById(nID := nId);

IF foundAlarm.Id = nId THEN
	DeactivateAlarm(alarm := GetAlarmById(nID := nId));
	RETURN;
END_IF


]]></ST>
      </Implementation>
    </Method>
    <Method Name="DeactivateAlarmByName" Id="{be438fb5-61fe-0976-2b73-63f968632459}" FolderPath="Methods - public\Actions - ByName\">
      <Declaration><![CDATA[(* Deactivates the alarm with the provided name (if it exists)*)
METHOD DeactivateAlarmByName
VAR_INPUT
	(* Alarm name to be deactivated.*)
	sName	: Tc2_System.T_MaxString;
END_VAR

VAR
	foundAlarm	 : ST_AlarmInfo;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT __ISVALIDREF(GetAlarmByName(sName := sName)) THEN
	RETURN;
END_IF

foundAlarm := GetAlarmByName(sName := sName);
IF foundAlarm.Name = sName THEN
	DeactivateAlarm(alarm := GetAlarmByName(sName := sName));
	RETURN;
END_IF


]]></ST>
      </Implementation>
    </Method>
    <Method Name="DeactivateAllAlarms" Id="{a535657e-4dea-0106-1f69-8617a9ced909}" FolderPath="Methods - Private\">
      <Declaration><![CDATA[METHOD PRIVATE DeactivateAllAlarms

VAR
	alarmIndex	: T_ArrayIndexer;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[FOR alarmIndex := _alarmArrayProperties.StartIndex TO GetMaxAlarmArrayIndex() BY 1 DO
	IF refAlarms[alarmIndex].IsActive THEN
		DeactivateAlarm(refAlarms[alarmIndex]);
	END_IF
END_FOR]]></ST>
      </Implementation>
    </Method>
    <Property Name="Enable" Id="{7512f5e5-e0f9-046c-0eb7-8f2b8ab7ee5c}" FolderPath="Properties - public\">
      <Declaration><![CDATA[{attribute 'monitoring':='call'}
(* Gets or sets if alarm manager is enabled or not. When disabled, all active alarms will become inactive!*)
PROPERTY Enable : BOOL
]]></Declaration>
      <Get Name="Get" Id="{918e4531-acfe-0d09-00fb-3fee96d6839a}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[Enable := _enable;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{f6a9f216-71b1-0a78-116c-6d3be0750bcc}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[IF NOT Enable AND _enable THEN 
	IF __ISVALIDREF(_logger) THEN
		_logger.LogInfo('Alarms are now disabled.');
	END_IF
	DeactivateAllAlarms();
	AcknowledgeAllAlarms();
ELSIF Enable AND NOT _enable THEN
	IF __ISVALIDREF(_logger) THEN
		_logger.LogInfo('Alarms are now enabled.');
	END_IF
END_IF

_enable := Enable;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="FB_init" Id="{2d9c09d1-e406-01ab-27dc-0d93a9ba2b35}" FolderPath="Constructor\">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
	bEnable	: BOOL;
	iLogger	: REFERENCE TO I_LoggerEx;	// If you do not want logging, simply set this variable to 0, internal checks will ommit any logging.
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[_enable := bEnable;
_logger REF= iLogger;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetAlarmById" Id="{04cf1bea-b90c-08ce-01da-5e78e3b14c19}" FolderPath="Methods - public\Actions - ById\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'variable'}
(* Gets the alarm with the provided id. 
 If no mathching alarm exists, an empty alarm is returned.*)
METHOD GetAlarmById : REFERENCE TO ST_AlarmInfo
VAR_INPUT
	(* Id of the alarm.*)
	nID	: T_ArrayIndexer;
END_VAR

VAR
	index	: T_ArrayIndexer;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT THIS^.ValidateArrayRef() THEN
	GetAlarmById REF= _emptyAlarm;
END_IF

FOR index := _alarmArrayProperties.StartIndex TO GetMaxAlarmArrayIndex() BY 1 DO
	IF refAlarms[index].Id = nId THEN
		GetAlarmById REF= refAlarms[index];
		RETURN;
	END_IF
END_FOR

GetAlarmById REF= _emptyAlarm;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetAlarmByName" Id="{7445463b-ef45-03e3-3b6b-6ac076786824}" FolderPath="Methods - public\Actions - ByName\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'variable'}
(* Gets the alarm with the provided name. 
 If no mathching alarm exists, an empty alarm is returned.*)
METHOD GetAlarmByName : REFERENCE TO ST_AlarmInfo
VAR_INPUT
	(* Name of the alarm*)
	sName	: Tc2_System.T_MaxString;
END_VAR

VAR
	index	 : T_ArrayIndexer;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT THIS^.ValidateArrayRef() THEN
	GetAlarmByName REF= _emptyAlarm;
END_IF

FOR index := _alarmArrayProperties.StartIndex TO GetMaxAlarmArrayIndex() BY 1 DO
	IF refAlarms[index].Name = sName THEN
		GetAlarmByName REF= refAlarms[index];
		RETURN;
	END_IF
END_FOR

GetAlarmByName REF= _emptyAlarm;]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetMaxAlarmArrayIndex" Id="{6421d87c-48b6-05a1-11d6-d58eb2e120b7}" FolderPath="Methods - Private\">
      <Declaration><![CDATA[METHOD PRIVATE GetMaxAlarmArrayIndex : T_ArrayIndexer
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[GetMaxAlarmArrayIndex := (_alarmArrayProperties.StartIndex + UDINT_TO_DINT(RegisteredAlarmsCount)) - 1;]]></ST>
      </Implementation>
    </Method>
    <Property Name="MaximumAlarmsCount" Id="{574f6a7f-c95b-020e-3b75-fa8d2b652f2d}" FolderPath="Properties - public\">
      <Declaration><![CDATA[{attribute 'monitoring':='call'}
(* Gets the maximum ammount of alarms that can be registered by the alarm manager (depends on provided buffer size)*)
PROPERTY MaximumAlarmsCount : UDINT
]]></Declaration>
      <Get Name="Get" Id="{201be11d-a4d3-06bc-373c-f5bf4b93201d}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[MaximumAlarmsCount := DINT_TO_UDINT(_alarmArrayProperties.Count);]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="RaiseAlarm" Id="{64969726-776b-059a-1fcd-7ef4a1062cdc}" FolderPath="Methods - Private\">
      <Declaration><![CDATA[METHOD INTERNAL RaiseAlarm
VAR_INPUT
	alarm : REFERENCE TO ST_AlarmInfo;
END_VAR
VAR
	message	: Tc2_System.T_MaxString;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF alarm.IsActive OR NOT _enable THEN
	RETURN;
END_IF

_activeAlarmCount := _activeAlarmCount + 1;
_unacknowledgedAlarmCount := _unacknowledgedAlarmCount + 1;
alarm.IsActive := TRUE;
alarm.IsAcknowledged := FALSE;
MEMSET(
	destAddr := ADR(alarm.TimeInfos),
	fillByte := 0,
	n := SIZEOF(alarm.TimeInfos));
	
alarm.TimeInfos.Activated := _dateTime.CurrentTime.Value;

IF __ISVALIDREF(_logger) THEN
	message := _stringBuilder.Reset()
		.AddString('Alarm ')
		.AddWithDblQuotationMarks(str := alarm.Name)
		.AddString(' with Id ')
		.AddWithDblQuotationMarks(str := TO_STRING(alarm.Id))
		.AddString(' actived! ')
		.AddString(alarm.Message)
		.Create();
		
	CASE alarm.Level OF 
		E_AlarmLevel.NONE: _logger.LogDefault(message := message);
		E_AlarmLevel.INFO: _logger.LogInfo(message := message);
		E_AlarmLevel.WARNING: _logger.LogWarning(message := message);
		E_AlarmLevel.ERROR: _logger.LogError(message := message);
	END_CASE
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="RaiseAlarmById" Id="{09fa4eeb-a927-062d-02d5-65db13becfa1}" FolderPath="Methods - public\Actions - ById\">
      <Declaration><![CDATA[(* Activates the alarm with the provided Id (if it exists)*)
METHOD RaiseAlarmById
VAR_INPUT
	(* Alarm id to be activated*)
	nId	: T_ArrayIndexer;
END_VAR

VAR
	foundAlarm	 : ST_AlarmInfo;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT __ISVALIDREF(GetAlarmById(nID := nId)) THEN
	RETURN;
END_IF

foundAlarm := GetAlarmById(nID := nId);
IF foundAlarm.Id = nId THEN
	RaiseAlarm(alarm := GetAlarmById(nID := nId));
	RETURN;
END_IF


]]></ST>
      </Implementation>
    </Method>
    <Method Name="RaiseAlarmByName" Id="{98ddc441-4696-0e5f-3229-fab0993563f1}" FolderPath="Methods - public\Actions - ByName\">
      <Declaration><![CDATA[(* Activates the alarm with the provided name*)
METHOD RaiseAlarmByName
VAR_INPUT
	(* Alarm name to be actived*)
	sName	: Tc2_System.T_MaxString;
END_VAR

VAR
	foundAlarm	 : ST_AlarmInfo;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT __ISVALIDREF(GetAlarmByName(sName := sName)) THEN
	RETURN;
END_IF

foundAlarm := GetAlarmByName(sName := sName);
IF foundAlarm.Name = sName THEN
	RaiseAlarm(alarm := GetAlarmByName(sName := sName));
	RETURN;
END_IF


]]></ST>
      </Implementation>
    </Method>
    <Method Name="RegisterAlarm" Id="{de1a4c4e-6dbf-08e7-0d35-b029c09f962e}" FolderPath="Methods - public\">
      <Declaration><![CDATA[(* Registers a new alarm with the provided information. 
 Returns TRUE if new alarm was registered or false if not (alarm buffer is full or the id already exists)*)
METHOD RegisterAlarm : BOOL
VAR_INPUT
	(* Alarm ID*)
	Id	: T_ArrayIndexer;
	(* Alarm name*)
	Name	: Tc2_System.T_MaxString;
	(* Message when the alarm occurrs*)
	Message	: Tc2_System.T_MaxString;
	(* Alarm level *)
	Level	: E_AlarmLevel;				
END_VAR

VAR
	foundAlarm		: ST_AlarmInfo;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _registeredAlarmsCount >= MaximumAlarmsCount THEN
	RETURN;
END_IF

foundAlarm := GetAlarmById(nID := Id);
IF foundAlarm.Id = Id THEN
	RETURN;
END_IF

refAlarms[_alarmArrayProperties.StartIndex + UDINT_TO_DINT(_registeredAlarmsCount)].Id := Id;
refAlarms[_alarmArrayProperties.StartIndex + UDINT_TO_DINT(_registeredAlarmsCount)].Name := Name;
refAlarms[_alarmArrayProperties.StartIndex + UDINT_TO_DINT(_registeredAlarmsCount)].Message := Message;
refAlarms[_alarmArrayProperties.StartIndex + UDINT_TO_DINT(_registeredAlarmsCount)].IsAcknowledged := TRUE;
refAlarms[_alarmArrayProperties.StartIndex + UDINT_TO_DINT(_registeredAlarmsCount)].Level := Level;
_registeredAlarmsCount := _registeredAlarmsCount + 1;
RegisterAlarm := TRUE;

]]></ST>
      </Implementation>
    </Method>
    <Property Name="RegisteredAlarmsCount" Id="{4f0a8335-36c0-03bf-1f9b-68a5187a4d02}" FolderPath="Properties - public\">
      <Declaration><![CDATA[{attribute 'monitoring':='call'}
(* Gets the total number of alarms registered*)
PROPERTY RegisteredAlarmsCount : UDINT
]]></Declaration>
      <Get Name="Get" Id="{bbf8a0aa-8a52-00a0-36a0-d4d1e1c279d6}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[RegisteredAlarmsCount := _registeredAlarmsCount;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="UnacknowledgedAlarmsCount" Id="{5532a814-2599-069d-1ecc-b91352519039}" FolderPath="Properties - public\">
      <Declaration><![CDATA[{attribute 'monitoring':='call'}
(* Gets the current count of alarms that have not yet been acknowledged*)
PROPERTY UnacknowledgedAlarmsCount : UINT
]]></Declaration>
      <Get Name="Get" Id="{5e00a12e-621e-096d-2039-095461d94ee9}">
        <Declaration><![CDATA[VAR
	alarmIndex		: T_ArrayIndexer;
	maxAlarmIndex	: T_ArrayIndexer;
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[maxAlarmIndex :=  GetMaxAlarmArrayIndex();
_unacknowledgedAlarmCount := 0;

FOR alarmIndex := _alarmArrayProperties.StartIndex TO maxAlarmIndex BY 1 DO
	IF NOT refAlarms[alarmIndex].IsAcknowledged THEN
		_unacknowledgedAlarmCount := _unacknowledgedAlarmCount + 1;
	END_IF
	
END_FOR

UnacknowledgedAlarmsCount := _unacknowledgedAlarmCount;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="ValidateArrayRef" Id="{5bf7baa0-d3ee-00ba-0fc2-1e40a79aa2c4}" FolderPath="Methods - Private\">
      <Declaration><![CDATA[METHOD PRIVATE ValidateArrayRef : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[ValidateArrayRef := refAlarms <> 0;]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>